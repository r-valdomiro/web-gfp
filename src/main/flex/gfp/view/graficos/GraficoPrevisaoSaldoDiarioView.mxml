<?xml version = "1.0" encoding = "utf-8"?>
<s:VGroup xmlns:fx = "http://ns.adobe.com/mxml/2009"
		  xmlns:s = "library://ns.adobe.com/flex/spark"
		  xmlns:mx = "library://ns.adobe.com/flex/mx"
		  xmlns:components = "com.fusioncharts.components.*"
		  xmlns:custom = "common.spark.component.*"
		  width = "100%"
		  height = "100%"
		  horizontalAlign = "center">
	<fx:Script>
		<![CDATA[
			import common.custom.ICustomEvent;
			import common.util.DateUtil;
			import common.util.NumberUtil;
			
			import gfp.dto.LancamentoDto;
			import gfp.event.AppEvent;
			import gfp.event.TransacaoEvent;
			import gfp.service.LancamentoService;
			
			import mx.collections.ArrayCollection;
			import mx.rpc.events.ResultEvent;
			
			import spark.events.IndexChangeEvent;
			
			[Dispatcher]
			public var dispatcher:IEventDispatcher;
			
			[Inject]
			[Bindable]
			public var service:LancamentoService;
			
			private function get chartData():ArrayCollection
			{
				var result:ArrayCollection = new ArrayCollection();
				
				if (service.listaPrevisaoSaldoDiario && service.listaPrevisaoSaldoDiario
					.length > 0)
				{
					var dto:Object;
					var sLink:String;
					var sTooltip:String;
					
					saldoMinimo = 0;
					
					for each (dto in service.listaPrevisaoSaldoDiario)
					{
						if (dto.saldoAcumulado < saldoMinimo)
						{
							saldoMinimo = dto.saldoAcumulado;
						}
						
						sTooltip = barTrocaModo.selectedIndex == 0 ? DateUtil.formatSimple(dto
																						   .dataCompensacao) :
							DateUtil.formatSimpleMonthYear(dto.dataCompensacao);
						sTooltip += "{br}Inicial: " + NumberUtil.formatNumber(dto
																			  .saldoInicial
																			  , 2);
						sTooltip += "{br}Receitas: " + NumberUtil.formatNumber(dto
																			   .receitas
																			   , 2);
						sTooltip += "{br}Despesas: " + NumberUtil.formatNumber(dto
																			   .despesas
																			   , 2);
						sTooltip += "{br}Saldo: " + NumberUtil.formatNumber(dto.saldoAcumulado
																			, 2);
						sLink = "E-itemClickHandler," + service.listaPrevisaoSaldoDiario
							.getItemIndex(dto);
						
						result.addItem({label: barTrocaModo.selectedIndex == 0 ?
											   dto.dataCompensacao.date.toString() :
											   DateUtil.formatSimpleMonthYear(dto
																			  .dataCompensacao)
										   , value: dto.saldoAcumulado, tooltext: sTooltip
										   , link: sLink});
					}
				}
				
				return result;
			}
			
			private function get chartParams():ArrayCollection
			{
				var result:ArrayCollection = new ArrayCollection();
				
				if (service.listaPrevisaoSaldoDiario && service.listaPrevisaoSaldoDiario
					.length > 0)
				{
					var params:Object = new Object();
					params.showValues = barTrocaModo.selectedIndex == 0 ? "0" : "1";
					params.forceDecimals = "0";
					params.formatNumber = "1";
					params.formatNumberScale = "0";
					params.decimals = "0";
					params.thousandSeparator = ".";
					params.decimalSeparator = ",";
					params.bgColor = "#eef4fb";
					params.baseFontColor = "#2074A3";
					params.toolTipBorderColor = "#2074A3";
					params.showBorder = "0";
					params.rotateLabels = "1";
					params.slantLabels = "1";
					params.divLineIsDashed = "1";
					params.palette = "3";
					params.showAlternateHGridColor = "0";
					params.caption = "Saldo Previsto";
					params.subCaption = "Próximos " + (barTrocaModo.selectedIndex ==
						0 ? "45 dias" : "12 meses");
					result.addItem(params);
				}
				
				return result;
			}
			
			private function get chartTrend():ArrayCollection
			{
				var result:ArrayCollection = new ArrayCollection();
				
				if (service.listaPrevisaoSaldoDiario && service.listaPrevisaoSaldoDiario
					.length > 0 && saldoMinimo < 0)
				{
					var params:Object = new Object();
					params.startValue = "0";
					params.endValue = saldoMinimo.toString();
					params.showOnToop = "0";
					params.isTrendZone = "1";
					params.displayvalue = " ";
					params.color = "ffecec";
					result.addItem(params);
				}
				
				return result;
			}
			
			public var grafico:GraficoTemplate;
			
			[Bindable]
			private var listaModos:ArrayCollection = new ArrayCollection([{label: "Diário"}
																		  , {label: "Mensal"}]);
			
			private var saldoMinimo:Number;
			
			[EventHandler(event = "AppEvent.ATUALIZAR_DASHBOARD")]
			public function atualizar(event:ICustomEvent = null):void
			{
				dispatcher.dispatchEvent(new TransacaoEvent(TransacaoEvent.LISTAR_PREVISAO_SALDO_DIARIO
															, barTrocaModo.selectedIndex +
															1, atualizar_Handler));
			}
			
			public function itemClickHandler(index:int):void
			{
				var item:Object = service.listaPrevisaoSaldoDiario.getItemAt(index);
				
				var dto:LancamentoDto = new LancamentoDto();
				dto.tipoPeriodo = 3;
				dto.dataInicio = item.dataCompensacao;
				dto.dataFinal = barTrocaModo.selectedIndex == 0 ? item.dataCompensacao :
					DateUtil.lastDayOfMonth(item.dataCompensacao.month, item.dataCompensacao
											.fullYear);
				dispatcher.dispatchEvent(new AppEvent(AppEvent.TO_TRANSACOES_DIA
													  , dto));
			}
			
			private function atualizar_Handler(re:ResultEvent):void
			{
				callLater(updateChart);
			}
			
			private function barTrocaModo_changeHandler(event:IndexChangeEvent):void
			{
				dispatcher.dispatchEvent(new TransacaoEvent(TransacaoEvent.LISTAR_PREVISAO_SALDO_DIARIO
															, event.newIndex + 1
															, atualizar_Handler));
			}
			
			private function updateChart():void
			{
				if (grafico)
				{
					removeElement(grafico);
				}
				
				grafico = new GraficoTemplate();
				addElement(grafico);
				grafico.render(this, GraficoTemplate.TypeLine, chartParams, chartData
							   , chartTrend);
			}
		]]>
	</fx:Script>

	<custom:CustomButtonBar id = "barTrocaModo"
							dataProvider = "{listaModos}"
							requireSelection = "true"
							height = "25"
							change = "barTrocaModo_changeHandler(event)"
							skinClass = "assets.skins.CustomButtonBarSkin"/>
</s:VGroup>
